DATA: File is regresija.dat;

VARIABLE:

NAMES ARE ID EKST_C UGOD_C SAV_C NE_C OTV_C HUM
IMP NEG_VAL COMPLEX TIP POS_VAL ADVER
SAV EKST A2 N2 OTV SEU;

USEVARIABLES ARE UGOD_C IMP NEG_VAL TIP 
ADVER SEU;

MISSING ARE ALL (-99);

CLUSTER IS id;

BETWEEN ARE UGOD_C;


ANALYSIS:
TYPE = TWOLEVEL;
ESTIMATOR = BAYES;
PROCESSORS = 12;  
ALGORITHM=GIBBS(RW); 
FBITERATIONS = 200000;


MODEL: 

%WITHIN%
IMP; 
NEG_VAL; 
TIP; 
ADVER; 
SEU; 

IMP WITH NEG_VAL; 
IMP WITH TIP; 
IMP WITH ADVER; 
IMP WITH SEU; 

NEG_VAL WITH TIP; 
NEG_VAL WITH ADVER; 
NEG_VAL WITH SEU;

TIP WITH ADVER; 
TIP WITH SEU;

ADVER WITH SEU;


%BETWEEN% 
IMP; 
NEG_VAL; 
TIP; 
ADVER; 
SEU; 
UGOD_C; 

SEU ON IMP (b1); 
SEU ON NEG_VAL (b2); 
SEU ON TIP (b3); 
SEU ON ADVER (b4); 


IMP ON UGOD_C (a1); 
NEG_VAL ON UGOD_C (a2); 
TIP ON UGOD_C (a3);  
ADVER ON UGOD_C (a4); 


SEU ON UGOD_C; 


IMP WITH NEG_VAL; 
IMP WITH TIP; 
IMP WITH ADVER; 

NEG_VAL WITH TIP; 
NEG_VAL WITH ADVER; 

TIP WITH ADVER;


MODEL CONSTRAINT:
NEW (ind_UV ind_UNV ind_UT ind_UN);

ind_UV = a1*b1; 
ind_UNV = a2*b2; 
ind_UT = a3*b3; 
ind_UN = a4*b4; 

 

OUTPUT: STANDARDIZED TECH8; 