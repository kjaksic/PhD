DATA: File is regresija.dat;

VARIABLE:

NAMES ARE ID EKST_C UGOD_C SAV_C NE_C OTV_C HUM
IMP NEG_VAL COMPLEX TIP POS_VAL ADVER
SAV EKST A2 N2 OTV SEU;

USEVARIABLES ARE SAV_C  
SAV EKST N2 OTV SEU;

MISSING ARE ALL (-99);

CLUSTER IS id;

BETWEEN ARE SAV_C;


ANALYSIS:
TYPE = TWOLEVEL;
ESTIMATOR = BAYES;
PROCESSORS = 12;  
ALGORITHM=GIBBS(RW); 
FBITERATIONS = 200000;


MODEL: 

%WITHIN% 
SAV; 
EKST; 
N2; 
OTV; 
SEU;

EKST WITH SAV; 
EKST WITH N2; 
EKST WITH OTV; 
EKST WITH SEU; 

SAV WITH N2; 
SAV WITH OTV; 
SAV WITH SEU; 

N2 WITH OTV;
N2 WITH SEU;

OTV WITH SEU;


%BETWEEN% 
SAV; 
EKST; 
N2; 
OTV; 
SEU; 
SAV_C; 


SEU ON EKST (b1); 
SEU ON N2 (b2); 
SEU ON SAV (b4); 
SEU ON OTV (b5);  


EKST ON SAV_C (a8); 
SAV ON SAV_C (a9); 
N2 ON SAV_C (a10); 
OTV ON SAV_C (a11);  

SEU ON SAV_C; 

EKST WITH SAV; 
EKST WITH N2; 
EKST WITH OTV; 

SAV WITH N2; 
SAV WITH OTV; 

N2 WITH OTV; 

MODEL CONSTRAINT:

NEW (ind_SE ind_SS 
ind_SN ind_SO);


ind_SE = a8*b1;
ind_SS = a9*b4;
ind_SN = a10*b2;
ind_SO = a11*b5;



OUTPUT: STANDARDIZED TECH8; 